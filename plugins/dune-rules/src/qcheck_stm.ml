open Utils

type config = {
  interface_file : string;
  config_file : string option;
  ocaml_output : string option;
  library : string option;
  package_name : string option;
  dune_output : string option;
  module_prefix : string option;
  submodule : string option;
  domain : bool;
  fork_timeout : int option;
  gen_alias : string option;
}

open Fmt

let get_optional proj suffix config =
  let default =
    str "%s_%s"
      Filename.(basename config.interface_file |> chop_extension)
      suffix
  in
  Option.value (proj config) ~default

let get_config_file = get_optional (fun cfg -> cfg.config_file) "config.ml"
let get_ocaml_output = get_optional (fun cfg -> cfg.ocaml_output) "tests.ml"
let qcheck_stm ppf _ = pf ppf "qcheck-stm"

let msg ppf config =
  pf ppf
    "; This file is generated by ortac dune qcheck-stm@\n\
     ; It contains the rules for generating and running QCheck-STM tests for %s@\n"
    config.interface_file

let interface ppf config = pf ppf "%s" config.interface_file
let config_file ppf config = pf ppf "%s" (get_config_file config)

let name ppf config =
  pf ppf "(name %s)" (Filename.chop_extension @@ get_ocaml_output config)

let public_name ppf config =
  pf ppf "(public_name %s)" (Filename.chop_extension @@ get_ocaml_output config)

let libraries =
  let library ppf config =
    pf ppf "%s@;"
      (Option.value config.library
         ~default:Filename.(basename config.interface_file |> chop_extension))
  in
  let k ppf config =
    let backend = if config.domain then "domain" else "sequential" in
    pf ppf
      "libraries@ %aqcheck-stm.stm@ qcheck-stm.%s@ qcheck-multicoretests-util@ \
       ortac-runtime-qcheck-stm.%s"
      library config backend backend
  in
  stanza k

let deps_pkg ppf = pf ppf "(deps@; %a)" (package "ortac-qcheck-stm")

let package config =
  match config.package_name with
  | None -> []
  | Some s -> [ (fun ppf _ -> pf ppf "(package %s)" s) ]

let module_prefix =
  optional_argument "--module-prefix" (fun cfg -> cfg.module_prefix)

let submodule = optional_argument "--submodule" (fun cfg -> cfg.submodule)
let domain cfg = if cfg.domain then [ (fun ppf _ -> pf ppf "--domain") ] else []

let gen_alias config =
  let alias = Option.value config.gen_alias ~default:"runtest" in
  fun ppf _ -> pf ppf "(alias %s)" alias

let gen_ortac_rule ppf config =
  let args =
    ortac
    :: qcheck_stm
    :: dep interface
    :: dep config_file
    :: quiet
    :: module_prefix config
    @ domain config
    @ submodule config
  in
  let run ppf = run ppf args in
  let run = stanza run in
  let action ppf =
    action_with_env "ORTAC_ONLY_PLUGIN" "qcheck-stm" ppf (with_target run)
  in
  let stanzas =
    [ gen_alias config; promote ]
    @ package config
    @ [ deps_pkg; targets get_ocaml_output; action ]
  in
  let rule ppf = rule ppf stanzas in
  stanza_rule rule ppf config

let gen_test_exe ppf config =
  let modules ppf config =
    pf ppf "(modules %s)" (Filename.chop_extension @@ get_ocaml_output config)
  in
  let test ppf =
    exe ppf @@ (name :: public_name :: package config) @ [ modules; libraries ]
  in
  stanza_rule test ppf config

let gen_dune_rules ppf config =
  let rules = [ msg; gen_ortac_rule; gen_test_exe ] in
  concat ~sep:cut rules ppf config
